// stores the tetris game. does everything but handling keypresses and what to do when the
// game fails
class Game {
	field int tileSize; // the size of each tile
	field int startingX, startingY; // the top-left corner of the grid

	// the columns of the field that contains all the blocks. note that we can't
	// use nested arrays because we can't do something like:
	// let playingField[0][0] = 1;
	// due to lack of nested arrays. we use 10 columns because a standard Tetris
	// game is a 10x20 grid
	field Array col1, col2, col3, col4, col5, col6, col7, col8, col9, col10; 

	// the number of frames until the next down default
	field int framesUntilDownDefault;

	// the number of frames until the next moveDown()
	field int framesUntilDown;

	// handles all the pieces
	field Array piecesList;

	// handles the next piece index. is incremented on every display. 
	field int nextPieceIndex;

	// handles the current piece itself and the holded piece.
	field Array currentPiece, holdedPiece;

	field int currentPieceIndex, holdedPieceIndex;

	// holds the current piece position
	field int currentPiecePosX, currentPiecePosY;

	// holds thes score
	field int score;

	// holds the amount of lines cleared
	field int lines;

	constructor Game new(int tileSizeArg) {
		var int i;
		var Array iBlock, jBlock, lBlock, sBlock, zBlock, oBlock, tBlock;
		var Array iBlock1, iBlock2, iBlock3, iBlock4;
		var Array jBlock1, jBlock2, jBlock3, jBlock4;
		var Array lBlock1, lBlock2, lBlock3, lBlock4;
		var Array sBlock1, sBlock2, sBlock3, sBlock4;
		var Array zBlock1, zBlock2, zBlock3, zBlock4;
		var Array oBlock1, oBlock2, oBlock3, oBlock4;
		var Array tBlock1, tBlock2, tBlock3, tBlock4;

		// assign tileSize
		let tileSize = tileSizeArg;

		// the starting x is placed so that the grid is centered in the middle. the board
		// width is 10 so the starting x has to be 5 tileSizes away from the game
		let startingX = 256 - (tileSize*5);

		// the starting y is the tileSize
		let startingY = tileSize;

		// the number of frames until the next moveDown starts at 20
		let framesUntilDownDefault = 20;
		let framesUntilDown = 20;

		// initialize the playing field
		let col1 = Array.new(20);
		let col2 = Array.new(20);
		let col3 = Array.new(20);
		let col4 = Array.new(20);
		let col5 = Array.new(20);
		let col6 = Array.new(20);
		let col7 = Array.new(20);
		let col8 = Array.new(20);
		let col9 = Array.new(20);
		let col10 = Array.new(20);

		// now set each cell to 0
		let i = 0;
		while (i < 10) {
			let col1[i] = 0;
			let col2[i] = 0;
			let col3[i] = 0;
			let col4[i] = 0;
			let col5[i] = 0;
			let col6[i] = 0;
			let col7[i] = 0;
			let col8[i] = 0;
			let col9[i] = 0;
			let col10[i] = 0;
			let i = i + 1;
		}

		let piecesList = Array.new(7);

		// nested arrays here are fine because we're not setting parts of it. we're
		// just iterating through it. note that it's tedious becaus we have to set
		// each part of the array.
		let iBlock = Array.new(4);
		let lBlock = Array.new(4);
		let jBlock = Array.new(4);
		let sBlock = Array.new(4);
		let zBlock = Array.new(4);
		let oBlock = Array.new(4);
		let tBlock = Array.new(4);

		// {piece}Block{num} means the x and y diff between the center
		// of the {piece} block and the {num}ᵗʰ block of the {piece} block.
		let iBlock1 = Array.new(2);
		let iBlock2 = Array.new(2);
		let iBlock3 = Array.new(2);
		let iBlock4 = Array.new(2);

		let lBlock1 = Array.new(2);
		let lBlock2 = Array.new(2);
		let lBlock3 = Array.new(2);
		let lBlock4 = Array.new(2);

		let jBlock1 = Array.new(2);
		let jBlock2 = Array.new(2);
		let jBlock3 = Array.new(2);
		let jBlock4 = Array.new(2);

		let sBlock1 = Array.new(2);
		let sBlock2 = Array.new(2);
		let sBlock3 = Array.new(2);
		let sBlock4 = Array.new(2);

		let zBlock1 = Array.new(2);
		let zBlock2 = Array.new(2);
		let zBlock3 = Array.new(2);
		let zBlock4 = Array.new(2);

		let oBlock1 = Array.new(2);
		let oBlock2 = Array.new(2);
		let oBlock3 = Array.new(2);
		let oBlock4 = Array.new(2);

		let tBlock1 = Array.new(2);
		let tBlock2 = Array.new(2);
		let tBlock3 = Array.new(2);
		let tBlock4 = Array.new(2);

		let iBlock1[0] = -1;
		let iBlock1[1] = 0;
		let iBlock2[0] = 0;
		let iBlock2[1] = 0;
		let iBlock3[0] = 1;
		let iBlock3[1] = 0;
		let iBlock4[0] = 2;
		let iBlock4[1] = 0;
		let lBlock1[0] = -1;
		let lBlock1[1] = -1;
		let lBlock2[0] = 0;
		let lBlock2[1] = -1;
		let lBlock3[0] = 1;
		let lBlock3[1] = -1;
		let lBlock4[0] = 1;
		let lBlock4[1] = 0;
		let jBlock1[0] = -1;
		let jBlock1[1] = 0;
		let jBlock2[0] = -1;
		let jBlock2[1] = -1;
		let jBlock3[0] = 0;
		let jBlock3[1] = -1;
		let jBlock4[0] = 1;
		let jBlock4[1] = -1;
		let sBlock1[0] = -1;
		let sBlock1[1] = 0;
		let sBlock2[0] = 0;
		let sBlock2[1] = 0;
		let sBlock3[0] = 0;
		let sBlock3[1] = 1;
		let sBlock4[0] = 1;
		let sBlock4[1] = 1;
		let zBlock1[0] = -1;
		let zBlock1[1] = 1;
		let zBlock2[0] = 0;
		let zBlock2[1] = 1;
		let zBlock3[0] = 0;
		let zBlock3[1] = 0;
		let zBlock4[0] = 1;
		let zBlock4[1] = 0;
		let oBlock1[0] = -1;
		let oBlock1[1] = 0;
		let oBlock2[0] = -1;
		let oBlock2[1] = 1;
		let oBlock3[0] = 0;
		let oBlock3[1] = 0;
		let oBlock4[0] = 0;
		let oBlock4[1] = 1;
		let tBlock1[0] = -1;
		let tBlock1[1] = 0;
		let tBlock2[0] = 0;
		let tBlock2[1] = 0;
		let tBlock3[0] = 0;
		let tBlock3[1] = 1;
		let tBlock4[0] = 1;
		let tBlock4[1] = 0;

		let iBlock[0] = iBlock1;
		let iBlock[1] = iBlock2;
		let iBlock[2] = iBlock3;
		let iBlock[3] = iBlock4;
		let lBlock[0] = lBlock1;
		let lBlock[1] = lBlock2;
		let lBlock[2] = lBlock3;
		let lBlock[3] = lBlock4;
		let jBlock[0] = jBlock1;
		let jBlock[1] = jBlock2;
		let jBlock[2] = jBlock3;
		let jBlock[3] = jBlock4;
		let sBlock[0] = sBlock1;
		let sBlock[1] = sBlock2;
		let sBlock[2] = sBlock3;
		let sBlock[3] = sBlock4;
		let zBlock[0] = zBlock1;
		let zBlock[1] = zBlock2;
		let zBlock[2] = zBlock3;
		let zBlock[3] = zBlock4;
		let oBlock[0] = oBlock1;
		let oBlock[1] = oBlock2;
		let oBlock[2] = oBlock3;
		let oBlock[3] = oBlock4;
		let tBlock[0] = tBlock1;
		let tBlock[1] = tBlock2;
		let tBlock[2] = tBlock3;
		let tBlock[3] = tBlock4;

		let piecesList[0] = iBlock;
		let piecesList[1] = lBlock;
		let piecesList[2] = jBlock;
		let piecesList[3] = sBlock;
		let piecesList[4] = zBlock;
		let piecesList[5] = oBlock;
		let piecesList[6] = tBlock;

		let holdedPiece = copyPiece(0);

		let holdedPieceIndex = 0;

		let currentPiece = copyPiece(0);

		let currentPiecePosX = 4;

		let currentPiecePosY = 1;

		let currentPieceIndex = 0;

		let score = 0;

		let lines = 0;

		return this;
	}

	// swaps the current piece and the holded piece. 
	method void holdPiece() {
		var int indexTemp;

		// swap the indices
		let indexTemp = currentPieceIndex;
		let currentPieceIndex = holdedPieceIndex;
		let holdedPieceIndex = indexTemp;

		// set the holded piece and current piece accordingly
		let holdedPiece = copyPiece(holdedPieceIndex);
		let currentPiece = copyPiece(currentPieceIndex);

		// reset the position
		let currentPiecePosX = 4;
		let currentPiecePosY = 1;

		return;
	}

	// copies any Tetris piece. Each tetris piece is in the form of: [
	// [int, int],
	// [int, int],
	// [int, int],
	// [int, int]
	// ]
	method Array copyPiece(int pieceIndex) {
		var Array piece;
		var Array newPiece, newPiece0, newPiece1, newPiece2, newPiece3;
		var Array piece0, piece1, piece2, piece3;
		let newPiece = Array.new(4);

		let piece = piecesList[pieceIndex];

		let newPiece0 = Array.new(2);
		let newPiece1 = Array.new(2);
		let newPiece2 = Array.new(2);
		let newPiece3 = Array.new(2);

		let piece0 = piece[0];
		let piece1 = piece[1];
		let piece2 = piece[2];
		let piece3 = piece[3];

		let newPiece0[0] = piece0[0];
		let newPiece0[1] = piece0[1];
		let newPiece1[0] = piece1[0];
		let newPiece1[1] = piece1[1];
		let newPiece2[0] = piece2[0];
		let newPiece2[1] = piece2[1];
		let newPiece3[0] = piece3[0];
		let newPiece3[1] = piece3[1];

		let newPiece[0] = newPiece0;
		let newPiece[1] = newPiece1;
		let newPiece[2] = newPiece2;
		let newPiece[3] = newPiece3;
		return newPiece;
	}

	// checks each line and makes sure it's not full.
	method void checkLines() {
		var int row, linesCleared;
		let row = 19;
		let linesCleared = 0;
		// iterate through each row backwards.
		while (row > -1) {
			// if the line is full, clear it. a true value means something is in it.
			if (col1[row] & 
				col2[row] & 
				col3[row] & 
				col4[row] & 
				col5[row] & 
				col6[row] & 
				col7[row] & 
				col8[row] & 
				col9[row] & 
				col10[row]) {
				let linesCleared = linesCleared + 1; // increment the lines cleared
				let framesUntilDownDefault = (framesUntilDownDefault+3)/2; // speed it up
				let framesUntilDown = framesUntilDownDefault;
				let col1[row] = 0; // clear it
				let col2[row] = 0;
				let col3[row] = 0;
				let col4[row] = 0;
				let col5[row] = 0;
				let col6[row] = 0;
				let col7[row] = 0;
				let col8[row] = 0;
				let col9[row] = 0;
				let col10[row] = 0;
			} else { // otherwise, set it to the linesCleared lines below to account for lines cleared
				let col1[row + linesCleared] = col1[row];
				let col2[row + linesCleared] = col2[row];
				let col3[row + linesCleared] = col3[row];
				let col4[row + linesCleared] = col4[row];
				let col5[row + linesCleared] = col5[row];
				let col6[row + linesCleared] = col6[row];
				let col7[row + linesCleared] = col7[row];
				let col8[row + linesCleared] = col8[row];
				let col9[row + linesCleared] = col9[row];
				let col10[row + linesCleared] = col10[row];
			}
			if (linesCleared > 0) {
				let col1[row] = 0;
				let col2[row] = 0;
				let col3[row] = 0;
				let col4[row] = 0;
				let col5[row] = 0;
				let col6[row] = 0;
				let col7[row] = 0;
				let col8[row] = 0;
				let col9[row] = 0;
				let col10[row] = 0;
			}
			let row = row - 1;
		}

		if (linesCleared = 1) {
			let score = score + 100;
			let lines = lines + 1;
		} if (linesCleared = 2) {
			let score = score + 300;
			let lines = lines + 2;
		} if (linesCleared = 3) {
			let score = score + 700;
			let lines = lines + 3;
		} if (linesCleared = 4) {
			let score = score + 1500;
			let lines = lines + 4;
		}

		return;
	}

	// displays all parts of the game and handles moving down every few frames
	method void display() {
		var int row;
		var String scoreString, linesClearedString;
		var String string;

		// check the lines
		do checkLines();

		// black background
		do Screen.setColor(true);
		do Screen.drawRectangle(192, 0, 511, 255);

		// title + documentation
		do Output.moveCursor(0, 0);
		let string = "TETRIS";
		do Output.printString(string);
		do string.dispose();
		do Output.println();
		let string = "left + hold: move left";
		do Output.printString(string);
		do string.dispose();
		do Output.println();
		let string = "right + hold: move right";
		do Output.printString(string);
		do string.dispose();
		do Output.println();
		let string = "down + hold: accelerate";
		do Output.printString(string);
		do string.dispose();
		do Output.println();
		let string = "up: set piece instantly";
		do Output.printString(string);
		do string.dispose();
		do Output.println();
		let string = "comma: rotate left";
		do Output.printString(string);
		do string.dispose();
		do Output.println();
		let string = "period: rotate right";
		do Output.printString(string);
		do string.dispose();
		do Output.println();
		let string = "backslash: hold piece";
		do Output.printString(string);
		do string.dispose();
		do Output.println();

		let scoreString = "score: ";
		let linesClearedString = "lines cleared: ";

		// display the score
		do Output.moveCursor(21, 0);
		do Output.printString(scoreString);
		do Output.printInt(score);
		do Output.println();
		do Output.printString(linesClearedString);
		do Output.printInt(lines);

		do scoreString.dispose();
		do linesClearedString.dispose();

		// outline of the grid: displays a little bit over where it's supposed to start at,
		// startingX, startingY, to a little bit over that, with more tileSizes and a 
		// padding of 5. 
		do Screen.setColor(false);
		do Screen.drawRectangle(startingX - 5, startingY - 5, startingX + (10*tileSize) + 5, 
			                    startingY + 5 + (20*tileSize));

		// now we display the actual grid
		do Screen.setColor(true);
		do Screen.drawRectangle(startingX, startingY, startingX + (10*tileSize), 
			                    startingY + (20*tileSize));

		// define the row that we're on
		let row = 0;

		// since we're displaying white cells, we set the color to white
		do Screen.setColor(false);

		// iterate through each row, which is basically each cell because there are 
		// individual columns already defined due to lack of nested arrays in Jack
		while (row < 20) {
			if (col1[row] = 0) { // display a white cell if the cell isn't already taken
				do Screen.drawRectangle(startingX + (0*tileSize) + 1, startingY + (tileSize*row) + 1, startingX + tileSize + (0*tileSize) - 1, startingY + tileSize + (tileSize*row) - 1);
			}
			if (col2[row] = 0) { // repeat for the rest, except 1 tileSize to the right
				do Screen.drawRectangle(startingX + (1*tileSize) + 1, startingY + (tileSize*row) + 1, startingX + tileSize + (1*tileSize) - 1, startingY + tileSize + (tileSize*row) - 1);
			}
			if (col3[row] = 0) { 
				do Screen.drawRectangle(startingX + (2*tileSize) + 1, startingY + (tileSize*row) + 1, startingX + tileSize + (2*tileSize) - 1, startingY + tileSize + (tileSize*row) - 1);
			}
			if (col4[row] = 0) { 
				do Screen.drawRectangle(startingX + (3*tileSize) + 1, startingY + (tileSize*row) + 1, startingX + tileSize + (3*tileSize) - 1, startingY + tileSize + (tileSize*row) - 1);
			}
			if (col5[row] = 0) { 
				do Screen.drawRectangle(startingX + (4*tileSize) + 1, startingY + (tileSize*row) + 1, startingX + tileSize + (4*tileSize) - 1, startingY + tileSize + (tileSize*row) - 1);
			}
			if (col6[row] = 0) { 
				do Screen.drawRectangle(startingX + (5*tileSize) + 1, startingY + (tileSize*row) + 1, startingX + tileSize + (5*tileSize) - 1, startingY + tileSize + (tileSize*row) - 1);
			}
			if (col7[row] = 0) { 
				do Screen.drawRectangle(startingX + (6*tileSize) + 1, startingY + (tileSize*row) + 1, startingX + tileSize + (6*tileSize) - 1, startingY + tileSize + (tileSize*row) - 1);
			}
			if (col8[row] = 0) {
				do Screen.drawRectangle(startingX + (7*tileSize) + 1, startingY + (tileSize*row) + 1, startingX + tileSize + (7*tileSize) - 1, startingY + tileSize + (tileSize*row) - 1);
			}
			if (col9[row] = 0) {
				do Screen.drawRectangle(startingX + (8*tileSize) + 1, startingY + (tileSize*row) + 1, startingX + tileSize + (8*tileSize) - 1, startingY + tileSize + (tileSize*row) - 1);
			}
			if (col10[row] = 0) {
				do Screen.drawRectangle(startingX + (9*tileSize) + 1, startingY + (tileSize*row) + 1, startingX + tileSize + (9*tileSize) - 1, startingY + tileSize + (tileSize*row) - 1);
			}

			// increment the row
			let row = row + 1;
		}

		return;
	}

	// moves current piece down by one tile
	method void moveDown() {
		let currentPiecePosY = currentPiecePosY + 1;

		// but this time, if it fails, then we set the piece in place in addition to
		// fixing it
		if (~(checkBorders())) {
			let currentPiecePosY = currentPiecePosY - 1;
			do setInPlace();
		}
		return;
	}

	// sets the current piece in place
	method void setInPlace() {
		// variable definition
		var Array currentOffset, colToSelect;
		var int i, xPos, yPos;
		var int aboveGroundBy;

		// make sure the next piece is not the current piece
		if (currentPieceIndex = nextPieceIndex) {
			let nextPieceIndex = nextPieceIndex + 1;
		}
		if (nextPieceIndex = 7) {
			let nextPieceIndex = 0;
		}
		let currentPieceIndex = nextPieceIndex;

		// move down all the way
		// in doing so, determine how much the piece is
		// above the landing spot to reward the player
		// for being quick.
		let aboveGroundBy = 0;
		while (checkBorders()) {
			let currentPiecePosY = currentPiecePosY + 1;
			let aboveGroundBy = aboveGroundBy + 1;
		}
		if (aboveGroundBy > 0) {
			let score = score + 1; // 1 point between 1 and 3
			if (aboveGroundBy > 3) {
				let score = score + 1; // 2 points between 4 and 5
				if (aboveGroundBy > 5) {
					let score = score + 1; // 3 points between 6 and 7
					if (aboveGroundBy > 7) {
						let score = score + 1; // 4 points between 8 and 9
						if (aboveGroundBy > 9) {
							let score = score + 1; // 5 points at 10
							if (aboveGroundBy > 10) {
								let score = score + 1; // 6 points at 11
								if (aboveGroundBy > 11) {
									let score = score + 1; // 7 points at 12
									if (aboveGroundBy > 12) {
										let score = score + 2; // 9 points at 13
										if (aboveGroundBy > 13) {
											let score = score + 3; // 12 points at 14
											if (aboveGroundBy > 14) {
												let score = score + 5; // 17 points at 15
												if (aboveGroundBy > 15) {
													let score = score + 8; // 25 points greater
												}
											}
										}
									}
								}
							} 
						} 
					}
				}
			}
		}

		// since after the loop, we're one more down than we can be, we have to adjust up
		let currentPiecePosY = currentPiecePosY - 1;

		// iterate through every offset in the list (there are 4)
		let i = 0;
		while (i < 4) {
			// set currentOffset to the current offset we're checking
			let currentOffset = currentPiece[i];

			// read displayCurrentFigure for documentation
			let xPos = currentPiecePosX + currentOffset[0];

			let yPos = currentPiecePosY + currentOffset[1];

			// set it into the playing field list!
			// but we have to figure out which column to select. read
			// checkBorders for documentation.
			if (xPos = 0) {
				let colToSelect = col1;
			}
			if (xPos = 1) {
				let colToSelect = col2;
			}
			if (xPos = 2) {
				let colToSelect = col3;
			}
			if (xPos = 3) {
				let colToSelect = col4;
			}
			if (xPos = 4) {
				let colToSelect = col5;
			}
			if (xPos = 5) {
				let colToSelect = col6;
			}
			if (xPos = 6) {
				let colToSelect = col7;
			}
			if (xPos = 7) {
				let colToSelect = col8;
			}
			if (xPos = 8) {
				let colToSelect = col9;
			}
			if (xPos = 9) {
				let colToSelect = col10;
			}

			// now we're actually setting the block
			let colToSelect[yPos] = -1;


			let i = i + 1;
		}

		// reset the current position to 4, 1
		let currentPiecePosX = 4;
		let currentPiecePosY = 1;

		let currentPiece = copyPiece(nextPieceIndex);
		let nextPieceIndex = 0;

		// display the game
		do display();
		do displayCurrentFigure();

		return;
	}

	// draw the current figure
	method void displayCurrentFigure() {
		var int i, xPos, yPos, temp;
		var boolean color, colorForFirstColumn;
		var int startingPositionY, startingPositionX;
		var int endPositionY, endPositionX;
		var Array currentOffset;

		// find the place where the piece is going to land
		let temp = currentPiecePosY;
		while (checkBorders()) {
			let currentPiecePosY = currentPiecePosY + 1;
		}
		let currentPiecePosY = currentPiecePosY - 1;

		// iterate through every offset in the list
		let i = 0;

		while (i < 4) {
			// for every offset, display it
			let currentOffset = currentPiece[i];

			// calculate the x position, the current piece x center plus the offset
			let xPos = currentPiecePosX + currentOffset[0];

			// calculate the y position, the current piece y center plus the offset
			let yPos = currentPiecePosY + currentOffset[1];

			// display it!
			// use a checkerboard style
			let colorForFirstColumn = true;
			let startingPositionX = startingX + (xPos*tileSize) + 1; 
			let startingPositionY = startingY + (yPos*tileSize) + 1; 
			let endPositionX = startingPositionX + tileSize - 1;
			let endPositionY = startingPositionY + tileSize - 1;
			let xPos = startingPositionX;
			let yPos = startingPositionY;
			while (yPos < endPositionY) {
				let color = colorForFirstColumn;
				while (xPos < endPositionX) {
					do Screen.setColor(color);
					do Screen.drawPixel(xPos, yPos);
					let color = ~color;
					let xPos = xPos + 1;
				}
				let colorForFirstColumn = ~colorForFirstColumn;
				let yPos = yPos + 1;
				let xPos = startingPositionX;
			}

			let i = i + 1;
		}



		let currentPiecePosY = temp;

		// we're drawing white now
		do Screen.setColor(false);

		// we're displaying the holded piece
		let i = 0;

		while (i < 4) {
			// for every offset, display it
			let currentOffset = holdedPiece[i];

			// calculate the x position, the current piece x center plus the offset
			let xPos = currentOffset[0];

			// calculate the y position, the current piece y center plus the offset
			let yPos = currentOffset[1];

			let startingPositionX = startingX + (10*tileSize) + 50 + (xPos*tileSize);
			let startingPositionY = startingY + (yPos*tileSize);

			// display it!
			do Screen.drawRectangle(startingPositionX + 1, startingPositionY + 1, startingPositionX + tileSize - 1, startingPositionY + tileSize - 1);

			let i = i + 1;
		}

		// we're drawing black now
		do Screen.setColor(true);

		let i = 0;

		while (i < 4) {
			// for every offset, display it
			let currentOffset = currentPiece[i];

			// calculate the x position, the current piece x center plus the offset
			let xPos = currentPiecePosX + currentOffset[0];

			// calculate the y position, the current piece y center plus the offset
			let yPos = currentPiecePosY + currentOffset[1];

			// display it!
			do Screen.drawRectangle(
				// the starting x position plus the tileSize times the xPos. note that it is 0-indexed
				startingX + (xPos*tileSize), 
				// same for y
				startingY + (yPos*tileSize),
				// now this time we repeat but one tileSize more
				startingX + (xPos*tileSize) + tileSize,
				startingY + (yPos*tileSize) + tileSize
				);

			let i = i + 1;
		}

		// display the center of rotation
		let startingPositionX = startingX + (currentPiecePosX*tileSize) + 2; 
		let startingPositionY = startingY + (currentPiecePosY*tileSize) + 2; 
		let endPositionX = startingPositionX + tileSize - 3;
		let endPositionY = startingPositionY + tileSize - 3;
		let xPos = startingPositionX;
		let yPos = startingPositionY;
		while (yPos < endPositionY) {
			let color = colorForFirstColumn;
			while (xPos < endPositionX) {
				do Screen.setColor(color);
				do Screen.drawPixel(xPos, yPos);
				let color = ~color;
				let xPos = xPos + 1;
			}
			let colorForFirstColumn = ~colorForFirstColumn;
			let yPos = yPos + 1;
			let xPos = startingPositionX;
		}

		let framesUntilDown = framesUntilDown - 1;

		if (framesUntilDown < 0) {
			let framesUntilDown = framesUntilDownDefault;
			do eraseCurrentFigure();
			do moveDown();
			do displayCurrentFigure();
		}

		let nextPieceIndex = nextPieceIndex + 1;
		if (nextPieceIndex = 7) {
			let nextPieceIndex = 0;
		}

		return;
	}

	method void dispose() {
		var Array piece, block;
		var int pieces, blocks;
		// iterate through every piece
		let pieces = 0;
		while (pieces < 7) {
			// iterate through every block in that piece
			let blocks = 0;
			let piece = piecesList[pieces];
			while (blocks < 4) {
				let block = piece[blocks];
				do block.dispose();
				let blocks = blocks + 1;
			}
			let pieces = pieces + 1;
			do piece.dispose();
		}
		do piecesList.dispose();
		do Memory.deAlloc(this);
		return;
	}

	// erase the current figure
	method void eraseCurrentFigure() {
		var int i, xPos, yPos, temp;
		var int startingPositionX, startingPositionY;
		var int endPositionX, endPositionY;
		var Array currentOffset;

		// center of rotation
		// display the center of rotation
		let startingPositionX = startingX + (currentPiecePosX*tileSize) + 2; 
		let startingPositionY = startingY + (currentPiecePosY*tileSize) + 2; 
		let endPositionX = startingPositionX + tileSize - 4;
		let endPositionY = startingPositionY + tileSize - 4;
		// display it!
		do Screen.drawRectangle(startingPositionX, startingPositionY, endPositionX, endPositionY);

		// find the place where the piece is going to land
		let temp = currentPiecePosY;
		while (checkBorders()) {
			let currentPiecePosY = currentPiecePosY + 1;
		}
		let currentPiecePosY = currentPiecePosY - 1;

		// iterate through every offset in the list
		let i = 0;

		// we're drawing white now
		do Screen.setColor(false);

		while (i < 4) {
			// for every offset, display it
			let currentOffset = currentPiece[i];

			// calculate the x position, the current piece x center plus the offset
			let xPos = currentPiecePosX + currentOffset[0];

			// calculate the y position, the current piece y center plus the offset
			let yPos = currentPiecePosY + currentOffset[1];

			// display it!
			do Screen.drawRectangle(
				// the starting x position plus the tileSize times the xPos. note that it is 0-indexed
				startingX + (xPos*tileSize) + 1, 
				// same for y
				startingY + (yPos*tileSize) + 1,
				// now this time we repeat but one tileSize more
				startingX + (xPos*tileSize) + tileSize - 1,
				startingY + (yPos*tileSize) + tileSize - 1
				);

			let i = i + 1;
		}

		let currentPiecePosY = temp;

		// iterate through every offset in the list
		let i = 0;

		while (i < 4) {
			// for every offset, display it
			let currentOffset = currentPiece[i];

			// calculate the x position, the current piece x center plus the offset
			let xPos = currentPiecePosX + currentOffset[0];

			// calculate the y position, the current piece y center plus the offset
			let yPos = currentPiecePosY + currentOffset[1];

			// display it!
			do Screen.drawRectangle(
				// the starting x position plus the tileSize times the xPos. note that it is 0-indexed
				startingX + (xPos*tileSize) + 1, 
				// same for y
				startingY + (yPos*tileSize) + 1,
				// now this time we repeat but one tileSize more
				startingX + (xPos*tileSize) + tileSize - 1,
				startingY + (yPos*tileSize) + tileSize - 1
				);

			let i = i + 1;
		}


		return;
	}

	// move the current figure left
	method void moveLeft() {
		let currentPiecePosX = currentPiecePosX - 1;
		if (~(checkBorders())) {
			let currentPiecePosX = currentPiecePosX + 1;
		}
		return;
	}

	// move the current figure right
	method void moveRight() {
		let currentPiecePosX = currentPiecePosX + 1;
		if (~(checkBorders())) {
			let currentPiecePosX = currentPiecePosX - 1;
		}
		return;
	}

	// checks the borders. returns true of the piece is valid, false if not. a piece is
	// valid if it is not beyond the edges of the playing field or intersects with one
	// of the pieces of the playing field.
	method boolean checkBorders() {
		var Array currentOffset;
		var int i, xPos, yPos;
		var Array colToCheck;

		let i = 0;
		while (i < 4) {
			let currentOffset = currentPiece[i];

			// calculate the xPos and yPos as in displayCurrentFigure
			let xPos = currentPiecePosX + currentOffset[0];

			let yPos = currentPiecePosY + currentOffset[1];

			// check if the xPos is too much to the left or right, if 
			// that does happen, move in the opposite direction.
			if (xPos < 0) {
				let currentPiecePosX = currentPiecePosX + 1;
				let xPos = xPos + 1;
				return checkBorders();
			}

			if (xPos > 9) {
				let currentPiecePosX = currentPiecePosX - 1;
				let xPos = xPos - 1;
				return checkBorders();
			}

			// if yPos is too much down, then return false. 
			if (yPos > 19) {
				return false;
			}

			// if none of those errors are there, then we check the
			// playing field. but we first have to check which column
			// we're checking.
			if (xPos = 0) {
				let colToCheck = col1;
			}
			if (xPos = 1) {
				let colToCheck = col2;
			}
			if (xPos = 2) {
				let colToCheck = col3;
			}
			if (xPos = 3) {
				let colToCheck = col4;
			}
			if (xPos = 4) {
				let colToCheck = col5;
			}
			if (xPos = 5) {
				let colToCheck = col6;
			}
			if (xPos = 6) {
				let colToCheck = col7;
			}
			if (xPos = 7) {
				let colToCheck = col8;
			}
			if (xPos = 8) {
				let colToCheck = col9;
			}
			if (xPos = 9) {
				let colToCheck = col10;
			}

			// now we're actually checking the column
			if (colToCheck[yPos]) {
				return false;
			}


			let i = i + 1;
		}

		return true;
	}

	// rotates the current piece right.
	method void rotateRight() {
		var Array newPiece, offsets, newOffsets, originalPiece;
		var int i, offsetX, offsetY;

		// iterate through each offset.
		let i = 0;
		let newPiece = Array.new(4);
		let originalPiece = currentPiece;
		while (i < 4) {
			let newOffsets = Array.new(2);
			let offsets = currentPiece[i];
			let offsetX = offsets[0];
			let offsetY = offsets[1];

			// set the new offset. 
			// up rotates to right
			let newOffsets[0] = offsetY;
			// right rotates to down
			let newOffsets[1] = -offsetX;

			let newPiece[i] = newOffsets;

			let i = i + 1;
		}
		let currentPiece = newPiece;
		if (checkBorders()) {
			// deAlloc() manually so that calling a function in the
			// same line as a list access doesn't take variables
			do Memory.deAlloc(originalPiece[0]);
			do Memory.deAlloc(originalPiece[1]);
			do Memory.deAlloc(originalPiece[2]);
			do Memory.deAlloc(originalPiece[3]);
			do originalPiece.dispose();
		}
		else {
			// deAlloc() manually so that calling a function in the
			// same line as a list access doesn't take variables
			let currentPiece = originalPiece; 
			do Memory.deAlloc(newPiece[0]);
			do Memory.deAlloc(newPiece[1]);
			do Memory.deAlloc(newPiece[2]);
			do Memory.deAlloc(newPiece[3]);
			do newPiece.dispose();
		}

		return;
	}

	// rotates the current piece left.
	method void rotateLeft() {
		var Array newPiece, offsets, newOffsets, originalPiece;
		var int i, offsetX, offsetY;

		// iterate through each offset.
		let i = 0;
		let newPiece = Array.new(4);
		let originalPiece = currentPiece;
		while (i < 4) {
			let newOffsets = Array.new(2);
			let offsets = currentPiece[i];
			let offsetX = offsets[0];
			let offsetY = offsets[1];

			// set the new offset. 
			// up rotates to left
			let newOffsets[0] = -offsetY;
			// left rotates to down
			let newOffsets[1] = offsetX;

			let newPiece[i] = newOffsets;

			let i = i + 1;
		}
		let currentPiece = newPiece;
		if (checkBorders()) {
			// deAlloc() manually so that calling a function in the
			// same line as a list access doesn't take variables
			do Memory.deAlloc(originalPiece[0]);
			do Memory.deAlloc(originalPiece[1]);
			do Memory.deAlloc(originalPiece[2]);
			do Memory.deAlloc(originalPiece[3]);
			do originalPiece.dispose();
		}
		else {
			// deAlloc() manually so that calling a function in the
			// same line as a list access doesn't take variables
			let currentPiece = originalPiece; 
			do Memory.deAlloc(newPiece[0]);
			do Memory.deAlloc(newPiece[1]);
			do Memory.deAlloc(newPiece[2]);
			do Memory.deAlloc(newPiece[3]);
			do newPiece.dispose();
		}

		return;
	}
}
